apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar
//                artifact javadocJar

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'Support extension to use Android ViewBinding quickly and smooth transform from kotlin-synthetic-extension'
                    url = 'https://github.com/cuongnv126/viewbindingdelegate'
                    licenses {
                        license {
                            name = 'Apache License 2.0'
                            url = 'https://github.com/cuongnv126/viewbindingdelegate/LICENSE.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'cuongnv'
                            name = 'Cuong V. Nguyen'
                            email = 'cuongnv126@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:github.com/cuongnv126/viewbindingdelegate.git'
                        developerConnection = 'scm:git:ssh://github.com/cuongnv126/viewbindingdelegate.git'
                        url = 'https://github.com/cuongnv126/viewbindingdelegate/tree/main'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}
